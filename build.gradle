plugins {
    id 'java'
    id 'jacoco'
}

jacoco {
    toolVersion = "0.8.10"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn subprojects*.test

    doFirst {
        def jacocoSubprojects = subprojects.findAll { it.plugins.hasPlugin('jacoco') }
        def execFiles = jacocoSubprojects.collect {
            def exec = file("${it.buildDir}/jacoco/test.exec")
            exec.exists() ? exec : null
        }.findAll { it != null }

        if (execFiles.isEmpty()) {
            throw new GradleException("No JaCoCo .exec files in any module - report will not be generated.")
        }

        def sourceDirs = jacocoSubprojects.collect { it.sourceSets.main.allSource.srcDirs }.flatten()
        def classDirs = jacocoSubprojects.collect { it.sourceSets.main.output.classesDirs }.flatten()

        executionData.setFrom(files(execFiles))
        sourceDirectories.setFrom(files(sourceDirs))
        classDirectories.setFrom(files(classDirs))
    }

    reports {
        html.required = true
        xml.required = true
        csv.required = true
    }
}

repositories {
    mavenCentral()
}